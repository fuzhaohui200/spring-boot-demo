group 'com.ymlinks'
version '1.0.0'


buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
        mybatisVersion = '1.2.0'
        grpcVersion = '1.12.0'
    }
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/repositories/public/"
        }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.5"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
repositories {
    maven {
        url "http://maven.aliyun.com/nexus/content/repositories/public/"
    }
    jcenter()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion") {
        exclude module: "jackson-databind"
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-undertow:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-cache:$springBootVersion")
    compile 'com.alibaba:fastjson:1.2.24'
    compile('mysql:mysql-connector-java:5.1.46')
    compile("org.springframework.boot:spring-boot-starter-data-redis:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb:$springBootVersion")
    compile("org.mybatis.spring.boot:mybatis-spring-boot-autoconfigure:$mybatisVersion")
    compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:$mybatisVersion")
    compile("com.alibaba:druid:1.0.28")
    compile 'log4j:log4j:1.2.17'
    compile "io.grpc:grpc-netty:$grpcVersion"
    compile "io.grpc:grpc-protobuf:$grpcVersion"
    compile "io.grpc:grpc-stub:$grpcVersion"

    compile "com.fasterxml.jackson.core:jackson-annotations:2.9.5"
    compile "com.fasterxml.jackson.core:jackson-core:2.9.5"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.5"
    compile "org.apache.commons:commons-pool2:2.5.0"

    compile "io.springfox:springfox-swagger2:2.8.0"
    compile "io.springfox:springfox-swagger-ui:2.8.0"

    compileOnly "org.projectlombok:lombok:1.16.20"

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
}

apply plugin: 'com.google.protobuf'

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
        }
    }
}

// Inform IntelliJ projects about the generated code.
apply plugin: 'idea'

idea {
    module {
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java")
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc")
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/main/proto'
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.7"
}
